/*
Copyright (C) 2019 Nikita Tseykovets <tseikovets@rambler.ru>
*/

:0
pln <h1 class="francis_spaight_h1">«Френсис Спейт»</h1>
pln <div class="francis_spaight_author">© Никита Цейковец</div>
pln Ваш корабль встретил в открытом море гонимое волнами и ветром потрёпанное судно с оборванными парусами и сломанными мачтами. С полузатопленной посудины, чудом пережившей ужасный шторм, вы сняли уцелевший экипаж, которому пришлось прибегнуть к крайней мере по морскому обычаю. Однако от одного из спасённых вы узнаёте, что морской обычай, возможно, оказался нарушен, и теперь вам предстоит решить, действительно ли они невиновны.
pln <!-- UUID://526F46C4-C9EB-44D5-86F5-CD642E4816F6// -->
pln <div class="francis_spaight_based">(По мотивам одноимённого рассказа Джека Лондона.)</div>
if count_1 < 1 then btn 1, Начать else btn 1, Ещё раз
perkill
end

:1
pln — Капитан, тут к вам просится [[юнга]] из бедолаг, которых мы вчера сняли с той полузатопленной посудины. — сказал старший помощник, заходя в каюту для традиционной вечерней рюмочки бренди. — Всё твердит, что якобы произошло мерзкое, подлое преступление. Возможно, мальчишка немного повредился в уме. Шутка ли, пережить такое, да ещё в столь юном возрасте. По мне, так эта история столь же понятна, как и печальна, так что и не знаю, стоит ли тратить на всё это время.
btn 1.1, — Что ж, думаю это не займёт много времени. Пригласите его в каюту.
btn 1.2, — Причём здесь я? Больных телом к лекарю, а больных душой к капеллану. Ха-ха! Гоните его в шею.
end

:1.1
pln — Заходи. — буркнул старший помощник, открыв дверь. — Капитан даёт тебе пять минут.
pln Через секунду в каюту проскользнул взъерошенный мальчишка лет двенадцати с копной ярко рыжих волос.
btn 1.1.1, — Как тебя зовут?
btn 2, — Что ты хотел мне рассказать?
end

:1.2
pln — Ступай прочь. — буркнул старший помощник, приоткрыв дверь и высунувшись наружу. — У капитана нет времени на твои глупости.
pln Однако в ту же секунду у него под рукой мелькнуло что-то красное, оказавшееся ярко рыжей головой взъерошенного мальчишки лет двенадцати.
pln — Сэр, это очень важно! — прокричал он, проскакивая в каюту и уворачиваясь от старшего помощника, пытавшегося его поймать за ворот матросской блузы. — Вы должны меня выслушать!
btn 1.1.1, — Кто ты такой, щенок?!
btn 2, — Какого дьявола?!.
end

:1.1.1
pln — Меня зовут Джордж Бёрнс. — торопливо проговорил мальчишка. — Я юнга с «Френсиса Спейта».
pln — На редкость настырный молодой человек. — проворчал старший помощник, отходя от двери.
goto 2

:2
pln — С вашего позволения, сэр, я бы хотел рассказать, как всё произошло на самом деле. — твёрдо сказал юнга, посмотрев исподлобья.
pln — Послушай, мальчуган. — обратился к нему старший помощник. — Я понимаю, что тебе довелось увидеть страшные вещи, но поверь, море жестоко, и чтобы выживать, у нас, моряков, есть столь же жестокие, но справедливые законы. Ещё без малого двести лет назад сам британский суд на Сент-Кристофер признал наш морской обычай и подтвердил допустимость такого в случае неизбежной необходимости.
pln — Я знаю, но здесь совсем не то. Здесь было подлое преступление!
btn 2.1, — То есть всё было не так, как рассказала ваша команда при спасении?
btn 2.2, — И кто же виновен в этом преступлении?
end

:2.1
p — Не то, чтобы не так, но всё было неправильно и несправедливо.
goto 3

:2.2
p — Не знаю, сэр, наверное, почти вся команда.
goto 3

:3
pln  — замялся юнга.
pln — Что за вздор?! — воскликнул старший помощник. — Капитан, мальчишка определённо повредился умом от всех злоключений, и сам не понимает, что говорит. По-моему, ему нужно просто отдохнуть и прийти в себя.
pln — Да нет же, я в полном рассудке! Выслушайте меня, сэр, и потом судите сами.
if count_1.1.1 < 1 then btn 4, Ладно, малец, расскажи всё с самого начала. else btn 4, — Ладно, Джордж, расскажи всё с самого начала.
end

:4
pln — Наш корабль шёл под одним [[крюйселем|крюйсель]], когда всё это произошло. — начал свой рассказ юнга. — Катастрофа случилась в три часа ночи: судно стало поворачиваться, а в это время волна подняла вверх его корму и обрушилась на неё. В следующее мгновение мы очутились в ложбине между волнами, накренившись в [[подветренную сторону|подветренная сторона]] так, что вода залила палубу вровень с [[комингсами люка|комингсы люка]], а через [[наветренный борт]] стал перекатываться вал за валом, так что ледяные потоки смели с палубы всё, что было на их пути.
goto 4.0

:4.0
if count_4.1 * count_4.2 > 0 then goto 5
if count_4.1 < 1 then btn 4.1, — Почему ваш рулевой не удержал корабль?
if count_4.2 < 1 then if count_4.1 < 1 then btn 4.2, — Как действовала команда? else btn 4.2, — Как действовала остальная команда?
btn 5, — В каком состоянии в итоге оказалось судно?
end

:4.1
pln — Наш рулевой, Майкл Биэйн из Лимерика, совсем ещё не бывал в море, если не считать перегонку плотов от квебекских судов к берегу в устье Шаннона. Он боялся огромных волн и обычно просто сползал вниз, прячась от грозящего удара.
pln — Чёрт побери! — выругался старший помощник. — Этот олух с перепуга видно отпустил ручки штурвального колеса, увидев большую волну. Корабль погиб из-за его недостойной моряка трусости! Типичное ирландское отр… — на этих словах он осёкся, бросив взгляд на ярко рыжую голову мальчишки, и просто раздражённо махнул рукой.
goto 4.0

:4.2
pln — По правде говоря, сэр, у нас практически не было ни одного хорошего моряка, так что беспомощные и отчаявшиеся матросы, растерявшись от страха и замешательства, вышли из повиновения и отказывались выполнять команды. Кто-то громко причитал, другие молча ёжились, вцепившись в [[ванты]] наветренного борта, третьи бормотали молитвы или выкрикивали гнусные проклятия, и ни капитан, ни помощник капитана не могли никого заставить взяться за откачку воды или поставить остатки парусов.
pln — Замечательный экипаж! — взорвался старший помощник. — В такой ситуации смелости у них хватает только на то, чтобы отказываться выполнять команды!
goto 4.0

:5
pln — Меньше чем через час судно легло набок, а мы вскарабкались на борт и повисли на снастях. — подавлено сказал юнга.
btn 6, — Какие приказы для спасения судна отдавал ваш капитан?
btn 6, — Вы срубили мачты, чтобы выправить корабль?
end

:6
pln — Самым опытным моряком на нашем корабле был помощник капитана. Когда судно начало крениться, он был в кормовой каюте и захлебнулся там так же, как и ещё два матроса под [[полубаком|полубак]]. Капитан же растерялся едва ли не больше, чем матросы. Проклиная нас за бездеятельность, он сам не предпринимал ничего. Рубить [[фок-|фок-мачта]] и [[грот-мачты|грот-мачта]] пришлось матросу из Белфаста по имени Маэни и юнге О'Брайену из Лимерика. Они сделали это, рискуя жизнью, стоя на круто наклонившейся палубе. В общем хаосе рухнула за борт и [[крюйс-стеньга]].
pln — Проклятье! Да кто вообще вас выпустил в море?! — рявкнул старший помощник, ударив кулаком по столу. — Вам всем надо было и дальше сидеть дома под юбками своих мамаш!
btn 6.1, — Как вы вообще не пошли ко дну, если практически ничего не делали для спасения судна?
btn 7, — В каком состоянии оказалось судно после шторма?
end

:6.1
pln — Мы возвращались в Лимерик с грузом древесины. Вода поднялась до палубы, так что если бы не лес в трюмах, то судно точно бы затонуло.
pln — Тысяча чертей! Да вам всем можно плавать не больше чем в кружке эля в каком-нибудь захолустном ирландском пабе, а не в Северной Атлантике, да к тому же в конце декабря! — выругался старший помощник, яростно дёрнув себя за бороду. — Ну и в каком состоянии вы в итоге вышли из шторма?
goto 7

:7
p — К утру над волнами выступала лишь корма, сломанная [[бизань-мачта]] да изуродованные [[фальшборты|фальшборт]]. Грот-мачта всё ещё держалась на 
if count_4.2 < 1 then p [[вантах|ванты]] else p вантах
pln  у борта и оглушительно била по корпусу, как огромная кувалда. Мы были еле живы от холода, но укрыться нам было негде. Волны, перекатываясь через судно, постоянно окатывали нас холодной солёной водой. В кормовой каюте воды было по колена, но там по крайней мере не гулял пронзительный ветер. В ней-то и собрались все оставшиеся в живых; мы стояли, прислонясь друг к другу и держась за что попало.
btn 7.1, — Были ли выставлены вахтенные?
btn 8, — Какими припасами располагал экипаж?
end

:7.1
p — Сначала Маэни пытался заставить других матросов нести поочерёдно вахту на [[марсе|марс]] бизань-мачты, но все предпочитали оставаться в каюте. Его сменял только юнга О'Брайен. В три часа дня он закричал, что видит парус. Мы вышли наружу и столпились на корме, облепив наветренные [[бизань-ванты]] и всматриваясь в незнакомое судно. Однако его курс проходил далеко, и когда оно исчезло за горизонтом, все вернулись в каюту, и ни один не вызвался сменить дозорного на марсе. К концу второго дня и Маэни с О'Брайеном отказались от своих попыток, и с этого времени судно дрейфовало без вахтенных.
goto 7.1.0

:7.1.0
if count_7.1.1 * count_7.1.2 > 0 then pln  — Припасы-то вы хоть попытались экономить? & goto 8 else pln
if count_7.1.1 < 1 then btn 7.1.1, — Маэни и О'Брайен — это те, кто рубил мачты?
if count_7.1.2 < 1 then btn 7.1.2, — Ты тоже отказывался нести вахту?
btn 8, — Какие припасы у вас оставались?
end

:7.1.1
pln — Да, сэр, они самые.
p — Единственные из вас, у кого в жилах текла настоящая солёная кровь, а не пресная болотная жижа! — воскликнул старший помощник, ударяя кулаком по раскрытой ладони.
goto 7.1.0

:7.1.2
pln — Ни я один, сэр, да и капитан нам ничего не приказывал. — потупил взгляд юнга. — На крохотной площадке марса было смертельно холодно, и жгучий ветер был слишком сильным испытанием для намного более опытных моряков, чем я. Добровольно вызвались только Маэни и О'Брайен.
p — Ваш капитан — полная размазня. — процедил сквозь зубы старший помощник.
goto 7.1.0

:8
pln — Всё продовольствие и пресная вода остались в затопленном трюме, и к ним не было никакого доступа. На всех было лишь три бутылки вина, а еды не было ни крошки. Пресную воду в небольших количествах мы набирали, подвешивая крышку от супового бачка под бизань-мачту, но дождь шёл редко, и нам приходилось нелегко. Кроме того, во время дождя мы пропитывали водой носовые платки, а потом выжимали их надо ртом или в свои башмаки. Когда шторм стихал, нам удавалось собирать тряпками воду с тех частей палубы, куда не захлёстывали волны, и таким способом увеличивать свои запасы воды. Но пищи у нас не было вовсе и не было никакой возможности её достать.
btn 8.1, — Сколько человек из экипажа уцелело?
btn 9, — Как долго продолжался шторм?
end

:8.1
p — В живых осталось тринадцать человек. — ответил юнга, слегка запнувшись. 
if count_12 < 1 then goto 9 else goto 12.0

:9
pln — Трое суток мы стояли по колено в бурлящей воде, полузамёрзшие, без еды. На четвёртый день шторм утих, и мы смогли наконец лечь на обсохший [[каютный настил]].
btn 9.1, — В каком состоянии находились люди?
btn 10, — В команде были ссоры?
end

:9.1
pln — Долгие часы, проведённые стоя по колено в морской воде, не прошли для нас даром: на ногах стали образовываться язвы. Эти язвы нестерпимо болели. Малейшее прикосновение вызывало жестокую боль, а в этой тесноте ослабевшие люди то и дело задевали друг друга. Стоило кому-нибудь пройти по каюте или просто встать, как ему вслед неслись стоны, оскорбления и проклятия.
pln — Ах вы чёртовы устрицы! — вскочил с места старший помощник. — Так вы там ещё и грызню устроили вместо того, чтобы работать?!
goto 10

:10
pln — Боюсь, что да, сэр. — подавлено ответил мальчишка. — Те, кто был сильнее, стали притеснять слабых, безжалостно прогоняя их с сухих мест в сырость и холод. Особенно плохо приходилось юнге О'Брайену.
btn 10.1, — О'Брайен ведь был не единственным юнгой?
btn 10.2, — О'Брайен был самым младшим из вас?
btn 11, — Почему особенно плохо приходилось О'Брайену?
end

:10.1
p — Кроме него, было ещё трое юнг, но именно О'Брайену доставалось больше всех. — ответил мальчишка. 
goto 11

:10.2
p — Нет, ему уже исполнилось пятнадцать. Он был самым старшим из юнг, но именно О'Брайену доставалось больше всех. — ответил мальчишка. 
goto 11

:11
pln — Он чаще других отстаивал свои права и восставал против мелких обид, которые матросы наносили юнгам. Поэтому каждый раз, когда О'Брайен подходил к матросам в поисках места посуше, где можно было бы выспаться, или просто проходил вблизи, его отталкивали, пинали, лягали. В ответ он проклинал их тупой эгоизм, и вновь на него сыпались удары, пинки и брань. С каждым днём матросы слабели, становились раздражительнее и злее, это лишний раз ухудшало их обращение с О'Брайеном.
btn 11.1, — Капитан пытался пресечь эти ссоры внутри команды?
goto 11.0

:11.0
btn 11.2, — Почему не было предпринято попытки разобрать палубу до трюма с припасами?
btn 12, — Сколько дней вы обходились без пищи?
end

:11.1
pln — Нет, сэр. Пожалуй, капитан и сам недолюбливал О'Брайена.
goto 11.0

:11.2
pln — Не знаю, сэр. — растеряно пробормотал юнга. — Корабль был почти полностью затоплен, а с палубы всё было смыто, так что у нас не было инструментов, да и, по правде говоря, возможно никто просто не догадался.
pln — И сколько же дней это продолжалось? — спросил старший помощник неожиданно спокойным голосом.
goto 12

:12
pln — Голод стал нестерпимым на семнадцатый день. С самого утра матросы собирались небольшими кучками и переговаривались вполголоса, время от времени бросая взгляды на О'Брайена. Ровно в полдень совещание подошло к концу. Капитан был выбран, чтобы высказать общее мнение. После того, как все собрались на [[юте|ют]], он сказал: «Матросы, вот уже две недели и два дня, как мы голодаем, а кажется, что прошло два года и два месяца. Так мы долго не протянем. Голодать дальше — выше человеческих сил. Нужно решить вопрос: что лучше — умереть всем или умереть одному. Все мы стоим на краю могилы. Если один из нас умрёт, остальные смогут жить, пока не встретится какое-нибудь судно. Что вы на это скажете?»
goto 12.0

:12.0
if count_8.1 * count_12.1 > 0 then goto 13 else if previous_loc = "8.1" then pln
if count_8.1 < 1 then btn 8.1, — Сколько человек к этому моменту уцелело после шторма?
if count_12.1 < 1 then btn 12.1, — Как к этому предложению отнеслась команда?
btn 13, — Как капитан предлагал это сделать?
end

:12.1
pln — Команда поддержала капитана. Майкл Биэйн, тот, что стоял у штурвала, когда «Френсис Спейт» потерял управление, выкрикнул: «Это верно!», а Салливен, матрос из Тарберта, крикнул: «Пускай это будет юнга!» Остальные также выразили своё согласие одобрительным ропотом.
pln — Лучше бы они с таким же рвением работали во время крушения. — недовольно буркнул старший помощник.
goto 12.0

:13
pln — Капитан сказал, что, если один из нас умрёт ради остальных, он сделает доброе дело, и матросы прервали его одобрительными криками: «Да, да! Доброе дело!» Ну а после этого он сказал: «Я считаю, что умереть лучше всего кому-то из юнг. У них нет семей, которые нужно кормить, и друзья их не будут оплакивать так, как нас — наши жёны и дети».
btn 13.1, — Были ли те, кто выступал против такого решения капитана?
goto 13.0

:13.0
btn 14, — Как был устроен жребий?
end

:13.1
pln — Юнги громко протестовали против такого решения. О'Брайен сказал: «Помирать нам не хочется так же, как и всем вам. И родных своих мы любим не меньше. А насчёт жён и детишек — так вон Майкл Биэйн сам из Лимерика, как и я, и хорошо знает, что моя мать — вдова и, кроме меня, о ней позаботиться некому. Это нечестно. Пусть жребий тянут все — и матросы и юнги».
btn 13.1.1, — Возражал ли кто-нибудь из более старших членов экипажа?
goto 13.0

:13.1.1
p — Из старших матросов один лишь Маэни выступил в защиту юнг, сказав, что по справедливости в жеребьёвке все должны участвовать на равных правах. Капитан и Салливен
if count_12.1 < 1 then p , матрос из Тарберта,
pln  настаивали на том, чтобы жребий тянули только юнги. Начался спор, но в его разгар Салливен обрушился на О'Брайена с криком: «Мы правильно сделаем, если покончим с тобой! Ты этого заслуживаешь. Мы с тобой расплатимся». Он подскочил к О'Брайену, да и ещё несколько матросов двинулись к юнге, протягивая к нему руки. О'Брайен отпрянул и, увёртываясь от них, закричал, что согласен на то, чтобы жребий тянули только юнги.
btn 13.1.1.1, — Матрос Маэни вмешивался в эту потасовку?
goto 13.0

:13.1.1.1
p — Нет, сэр. — тихо ответил юнга, после чего вздохнул и продолжил. 
goto 14

:14
p — Капитан выбрал четыре щепки разной длины и подал их Салливену, 
if count_12.1 + count_13.1.1 < 1 then p матросу из Тарберта
p . Тот ухмыльнулся и обратился к О'Брайену
if count_13.1 < 1 then p , который был недоволен тем, что в жеребьёвке участвуют только юнги
pln : «Ты, может быть, думаешь, что жеребьёвка будет нечестной? Ну, что ж, тогда ты сам будешь назначать жребий». О'Брайен согласился, после чего ему накрепко завязали носовым платком глаза, и он встал на колени, повернувшись спиной к Салливену. «Умрёт тот, кому ты назначишь самую короткую щепку», — сказал капитан.
btn 14.1, — Возражал ли кто-нибудь против такого способа жеребьёвки?
btn 15, — Как проходила жеребьёвка?
end

:14.1
p — Нет, сэр. — ответил юнга. 
goto 15

:15
pln — Салливен поднял вверх одну из щепок и спросил: «Чья это будет щепка?» «Маленького Джонни Шиэна», — ответил О'Брайен. Салливен отложил щепку в сторону и достал следующую: «А это чья щепка?» «Джорджа Бёрнса», — сказал О'Брайен… Это значит моя. — прервался мальчишка, шмыгнув носом. — Вторая щепка была отложена в сторону, как и первая. Тут Салливен поднял третью: «А эта для кого?» «Для меня», — ответил О'Брайен. В ту же секунду Салливен выкрикнул: «Ну, так ты сам себе её и выбрал!»
goto 15.0

:15.0
if previous_loc = "15.1.1" or previous_loc = "15.2.1" then if count_15.1 * count_15.2 > 0 then goto 16 else pln
if count_15.1 < 1 then btn 15.1, — Хорошо ли были видны щепки в руках Салливена?
if count_15.2 < 1 then btn 15.2, — Как остальная команда восприняла результаты жеребьёвки?
btn 16, — Как отреагировал сам О'Брайен?
end

:15.1
pln — Не очень хорошо, сэр. Когда Салливен поднимал вверх щепку, понять, короткая она или нет, было нельзя, потому что остальные щепки в этот момент он прятал в руке. Да и когда он откладывал отобранные щепки в сторону, их было уже невидно, так что невозможно было понять, была ли это та щепка, которая означала смерть. Ну а в конце он сразу быстрым движением смешал все щепки в кучу, из-за чего было трудно что-то рассмотреть.
btn 15.1.1, — Неужели, Джордж, ты не постарался хорошенько рассмотреть даже ту щепку, которая предназначалась тебе?
goto 15.0

:15.1.1
p — Нет, сэр, я её не видел. — ответил юнга, слегка запнувшись. 
goto 15.0

:15.2
pln — Сразу после слов Салливена, несколько матросов заговорили вполголоса: «Хорошо. Правильно». Против никто не высказывался.
btn 15.2.1, — Матрос Маэни тоже не выказал никакого несогласия?
goto 15.0

:15.2.1
p — Нет, сэр. Маэни стоял молча в стороне. — тихо проговорил мальчишка. 
goto 15.0

:16
p — Сам О'Брайен был очень спокоен. Он встал на ноги, снял повязку, огляделся и спросил: «Где она? Где короткая щепка? Та, что я выбрал для себя?» В ответ капитан молча указал на четыре щепки, лежащие на палубе
if count_15.1 < 1 then p , которые туда бросил Салливен
pln .
btn 16.1, — То есть О'Брайен сомневался в честности жеребьёвки?
goto 16.0

:16.0
btn 17, — Как вела себя команда?
end

:16.1
pln — Думаю, да, сэр. О'Брайен сразу спросил команду: «Откуда вы знаете, что моя щепка короткая? Кто-нибудь её видел?»
btn 16.1.1, — Ему ответил кто-нибудь на этот вопрос?
goto 16.0

:16.1.1
pln — Нет, ему никто ничего не ответил. — тихо сказал юнга. — О'Брайен спросил самого младшего из всех нас: «Джонни Шиэн, ты её видел?», но Джонни молчал. Тогда он спросил Маэни: «А ты видел её?», но тот ответил: «Нет, я её не видел».
goto 16.1.1.0

:16.1.1.0
if count_16.1.1.1 * count_16.1.1.2 * count_16.1.1.3 > 0 then pln  — Продолжай. Как себя вела остальная команда? & goto 17 else pln
if count_16.1.1.1 < 1 then btn 16.1.1.1, — Почему О'Брайен не спросил тебя?
if count_16.1.1.2 < 1 then btn 16.1.1.2, — Маэни действительно ничего не видел?
if count_16.1.1.3 < 1 then btn 16.1.1.3, — Почему младший юнга промолчал? Он мог видеть щепки?
goto 16.0

:16.1.1.1
pln — Я не знаю, сэр. Если вы думаете, что мы с ним ссорились, то ничего такого не было. — на глазах мальчишки появились слёзы.
p — Ничего мы не думаем. Капитан просто хочет разобраться. — вмешался старший помощник.
goto 16.1.1.0

:16.1.1.2
pln — Не могу сказать, сэр. Он стоял в стороне. — ответил юнга, слегка замявшись.
p — Мог подойти и поближе, если бы хотел увидеть. — буркнул старший помощник.
goto 16.1.1.0

:16.1.1.3
pln — Не знаю, сэр. Возможно, он просто сильно испугался. — ответил юнга.
p — [[Sua tunica propior pallio]]. — пробормотал старший помощник.
goto 16.1.1.0

:17
pln — Матросы тихо переговаривались и недовольно ворчали. «Жеребьёвка была правильной. Ты мог выиграть, но ты проиграл — вот и всё», — наконец сказал Салливен. Его поддержал и капитан: «Честная жеребьёвка. Я сам видел. Короткая щепка досталась тебе, О'Брайен, и ты бы лучше готовился».
btn 17.1, — Получается, в команде никто кроме О'Брайена не выказывал явного недовольства?
btn 18, — Кто должен был выполнить печальную обязанность?
end

:17.1
pln — Явно никто, сэр. Правда, потом О'Брайен выкрикнул: «Это подлое убийство!» После этого Маэни заявил: «Я не притронусь к нему. Я не съем ни куска». На это Салливен насмешливо заметил: «Тогда твоя доля достанется другим, не таким трусам, как ты. Делай своё дело, [[кок]]!»
pln — Это должен был сделать кок? — тихо спросил старший помощник.
p — Да, сэр. — ответил юнга. 
goto 18

:18
p — Капитан позвал 
if count_17.1 < 1 then p [[кока|кок]] else p кока
pln : «Горман, поди сюда. Эй, кто-нибудь, принесите крышку от бачка! Горман, выполняй свой долг, как подобает мужчине». Однако кок нерешительно запротестовал: «Но как я это сделаю? Убивать мальчишек не моё дело».
btn 18.1, — Как в этот момент вёл себя О'Брайен?
btn 19, — То есть кок отказался выполнять эту работу?
end

:18.1
pln — О'Брайен молчал и встревоженно прислушивался к разговору капитана и кока. Он был бледен. Губы его дёргались, а по временам всё его тело била дрожь. Младший юнга Джонни Шиэн так и вовсе заплакал.
pln — А как команда отнеслась к отказу кока? — спросил старший помощник.
goto 19

:19
p — Биэйн
if count_12.1 < 1 then p , тот, что стоял у штурвала, когда «Френсис Спейт» потерял управление,
pln  обратился к коку с явной угрозой: «Если ты нам не хочешь помочь, обойдёмся без тебя! Кто-нибудь должен будет умереть — не он, так ты!» Горман продолжал отказываться: «Я нанимался на должность кока. Был бы камбуз, я бы в камбузе и работал. Но марать руки убийством я не буду. Такого пункта в контракте нет. Я кок…» Однако Салливен схватил кока за голову и круто заломил её назад со словами: «Кок?.. Да ты им останешься не больше минуты! Где твой нож, Майк? Давай-ка его сюда».
btn 19.1, — Кто-то из команды или капитан пробовали вмешаться?
btn 20, — Как вёл себя Горман?
end

:19.1
p — Нет, сэр, но всё произошло очень быстро. — ответил юнга. 
goto 20

:20
pln — Почувствовав прикосновение стали, Горман захныкал: «Я согласен, но только подержите юнгу!» Он был подслеповат и нерешителен, так что трудно было ожидать от него твёрдости. — заметил мальчишка.
btn 20.1, — Как держался О'Брайен?
btn 21, — Что дальше делал Горман?
end

:20.1
pln — О'Брайен был достаточно спокоен и даже обратился к коку: «Всё в порядке, Горман, можешь начинать. Я-то ведь знаю, что ты не хочешь этого делать». А потом ещё сказал капитану, чья рука крепко сдавила его плечо: «Всё в порядке, сэр. Вам не нужно держать меня, сэр. Я буду стоять спокойно».
pln — Вот это настоящий моряк! — воскликнул старший помощник, хлопнув себя по колену. — Ну и что кок?..
goto 21

:21
p — Горману дали уже раскрытый перочинный нож капитана, а Биэйн приказал маленькому Джонни Шиэну, который ревел в голос, перестать галдеть и принести крышку от бачка, сопроводив эти слова затрещиной. О'Брайен снял куртку и оголил правую руку до плеча. Его нижняя губа 
if count_18.1 > 0 then p всё ещё 
pln дрожала, но он держался изо всех сил. В этот момент он попросил: «Маэни, если ты вернёшься домой, расскажи моей матери, что со мной случилось». Маэни кивнул. А всем остальным О'Брайен сказал: «Это — мерзкое, подлое убийство. Не ждите добра от крови юнги. Попомните мои слова: проку от него никому из вас не будет».
btn 21.1, — Эта была та самая крышка от бачка?
goto 21.0

:21.0
if count_21.2 > 0 then goto 22 else if previous_loc = "21.1" then pln
btn 21.2, — Кто-нибудь держал О'Брайена?
btn 22, — Кто именно находился рядом и принимал участие, помимо Гормана?
end

:21.1
p — Да, сэр, та самая крышка от супового бачка, которую мы подвешивали под бизань-мачту для сбора дождевой воды. — ответил юнга. — Маленький Джонни Шиэн принёс её, шатаясь и падая, так он ослабел от голода; по его щекам катились слёзы. Биэйн взял у него крышку и снова ударил его.
goto 21.0

:21.2
p — По началу О'Брайена держал капитан, а также рядом стоял Салливен и Горман, но потом его обступила почти вся команда. — сказал юнга. 
goto 21.0

:22
pln — За исключением Маэни, все матросы окружили О'Брайена, вытягивая шеи, чтобы лучше видеть. Юнги стояли поодаль, плача и всхлипывая. Капитан приказал: «Приготовьтесь! Ты, Салливен, держи крышку — вот так, вплотную. Не пролей ничего. Это дорогая штука».
btn 22.1, — О'Брайен в этот момент пробовал протестовать или сопротивляться?
btn 22.2, — Нож был в руках у Гормана?
end

:22.1
p — Нет, сэр. — ответил юнга. 
goto 23

:22.2
p — Да, сэр. — ответил юнга. 
goto 23

:23
pln — Горман начал. Лезвие было тупое, а он был вконец измотан. Кроме того, его рука тряслась так неистово, что он чуть не уронил нож. Заметив это, капитан предостерегающе сказал: «Будь мужчиной, Горман!» После этого кок с отчаянной решимостью принялся пилить лезвием кисть О'Брайена. Наконец вены были перерезаны. Салливен подставил крышку от бачка. Разрезы широко зияли, но из них не лился ярко-красный поток. Крови не было вовсе. Вены были пустыми, иссякшими.
btn 23.1, — Ты хорошо это видел?
btn 24, — Как на это отреагировали люди?
end

:23.1
p — Да, сэр. Никто из нас не мог отвести глаз от этого непостижимого и чудовищного зрелища: пустые, обескровленные вены человека, который ещё был жив. В этот момент 
goto 24

:24
pln Маэни выкрикнул: «Это — предостережение свыше! Оставьте юнгу в покое. Попомните мои слова. Его смерть не поможет вам», но капитан проговорил охрипшим, изменившимся голосом, обращаясь к Горману: «Попробуй у локтя, у левого локтя, он ближе к сердцу». Сам же О'Брайен был хладнокровен. «Дай мне нож, — сурово сказал он и взял его из руки кока. — Я не могу смотреть, как ты мучаешь меня». О'Брайен совершенно спокойно надрезал вену у левого локтя, но и ему не удалось вызвать кровотечение.
btn 24.1, — Настроения в команде после всего этого не поменялись?
btn 24.2, — О'Брайен вообще больше не протестовал и не сопротивлялся?
end

:24.1
p — Нет, сэр, скорей наоборот, они стали более решительными. 
goto 25

:24.2
p — Нет, сэр, он просто держался из последних сил, но потом всё-таки не выдержал. 
goto 25

:25
pln Салливен сказал: «Всё это зазря. Надо пустить ему кровь из горла, чтобы он не мучился». Однако для О'Брайена это было уже слишком, и он закричал: «Не режьте горло! В горле тоже не будет крови. Дайте мне немного отдохнуть. Это оттого, что я замёрз и ослабел. Позвольте мне лечь и поспать немного. Тогда я согреюсь, и кровь потечёт».
btn 25.1, — О'Брайен спорил или говорил это просящим тоном?
btn 26, — Что ему ответили?
end

:25.1
p — Просящим, сэр. Он еле сдерживал слёзы. — ответил юнга прервавшимся голосом. 
goto 26

:26
pln — Салливен возразил: «Бесполезно. Как будто ты сейчас сможешь спать! Ты и не заснёшь, и не согреешься. Посмотри на себя. Тебя же бьёт озноб». В ответ ему О'Брайен торопливо заговорил: «Однажды в Лимерике я заболел, и доктор не смог отворить мне кровь. Но после того, как я поспал несколько часов и согрелся в постели, кровь потекла свободно. Я вам говорю святую правду, не убивайте меня». Однако тут вмешался капитан: «Его вены вскрыты. Нет смысла оставлять его в покое, когда он мучается. Давайте кончать с этим сейчас же».
btn 26.1, — Остальная команда была настроена также?
btn 26.2, — В этот момент О'Брайен пытался сопротивляться? Его кто-нибудь держал?
end

:26.1
p — Да, сэр, за исключением юнг и Маэни, которые молча стояли в стороне. П
goto 27

:26.2
p — Нет, сэр, в тот момент они просто разговаривали и О'Брайена уже никто не держал. Однако п
goto 27

:27
pln осле этого матросы двинулись к О'Брайену, но он отскочил в сторону и пронзительно закричал: «Я вас сживу со света! Убери руки, Салливен! Я ещё вернусь! Я буду вам являться — во сне и наяву я буду вам являться, пока вы не подохнете!» В ответ Биэйн заорал: «Это позор! Если бы короткая щепка досталась мне, я бы дал ребятам отрубить себе голову и умер спокойно». Салливен прыгнул в круг и схватил О'Брайена за волосы, а за ним ринулись остальные матросы.
btn 27.1, — О'Брайен сопротивлялся?
btn 27.2, — Кто-нибудь из команды вмешался на стороне О'Брайена? Может быть Маэни?
end

:27.1
p — Да, сэр.
goto 28

:27.2
p — Нет, сэр. Маэни стоял молча в стороне.
goto 28

:28
pln  О'Брайен отчаянно отбивался руками и ногами, кусал тех, кто его держал. Маленький Джонни Шиэн неистово завопил, но матросы не обращали на него никакого внимания. О'Брайена повалили навзничь на палубу, а крышку от бачка подставили под голову. Гормана вытолкнули вперёд. Кто-то отстегнул от пояса большой нож и всунул его в руку Гормана. «Делай своё дело! Живее!» — закричали матросы.
btn 28.1, — Чей это был нож?
btn 28.2, — Гормана опять пришлось заставлять силой?
end

:28.1
pln — Не могу сказать, сэр. — ответил юнга замявшись. — В толчее было трудно рассмотреть.
pln — Ну а что кок? — спросил старший помощник охрипшим голосом.
goto 29

:28.2
p — Да, сэр. — ответил юнга. 
goto 29

:29
pln — Вытолкнутый вперёд Горман склонился над О'Брайеном, но, встретив его взгляд, дрогнул и опустил нож. Увидевший это Биэйн заорал: «Ну же! Или я убью тебя собственными руками». На кока посыпался град ругательств и угроз, но он всё равно не решался сделать то, что от него требовали. Глядя на замершего Гормана, Салливен произнёс с угрозой: «Может быть, в его жилах окажется больше крови, чем у О'Брайена».
btn 29.1, — Почему Биэйн или Салливен сами не взялись сделать то, на что не решался Горман?
btn 29.2, — Насколько серьёзно сам Горман воспринимал угрозы, ведь пока все лишь пытались переложить ответственность за насилие друг на друга?
end

:29.1
p — Не знаю, сэр. Думаю, они боялись этого не меньше Гормана, поэтому хотели заставить его.
goto 30

:29.2
p — Горман выглядел испуганным, да и ему угрожали не только на словах.
goto 30

:30
pln  — ответил юнга. — Биэйн схватил Гормана за волосы, заломил его голову назад и оттащил в сторону, а в это время Салливен попытался отнять у кока нож. Однако Горман отчаянно вцепился в рукоять ножа и исступлённо завизжал: «Пустите, я сам всё сделаю! Не режьте мне горло! Я сделаю это!»
btn 30.1, — Никто из матросов или капитан по-прежнему не вмешивались?
btn 30.2, — Действительно ли Горман не мог отказаться, ведь даже самые горячие Биэйн и Салливен, боялись пролить кровь О'Брайена?
end

:30.1
p — Никто, сэр. — тихо
goto 31

:30.2
p — Не знаю, сэр. Он был напуган и вряд ли в этот момент рассуждал. —
goto 31

:31
pln  ответил юнга. — Горман, не сопротивляясь, позволил снова вытолкнуть себя вперёд. Взглянув на О'Брайена, он закрыл глаза и стал бормотать молитву. Потом, не открывая глаз, он совершил то, что от него требовали. О'Брайен издал пронзительный крик, быстро сменившийся булькающим всхлипыванием. Матросы держали его до тех пор, пока агония не прекратилась, после чего тело опустили на палубу.
pln Старший помощник шумно выдохнул и разжал кулаки с побелевшими костяшками, как будто до последнего момента ждал в напряжении, что история, финал которой ему был прекрасно известен, в этот раз закончится по-другому.
btn 31.1, — Были ли те, кто потом отказался от пищи?
btn 32, — Как вела себя команда после этого?
end

:31.1
p — До этого вообще не дошло, сэр. — ответил юнга дрогнувшим голосом. 
goto 32

:32
pln — Матросы были возбуждены и полны нетерпения; с проклятиями и угрозами они заставляли Гормана быстрее приготовить им еду. Но тут Маэни негромко сказал: «Довольно, проклятые убийцы. Довольно, говорю я вам. Теперь вам ничего не нужно. Я ведь говорил: кровь этого парня не принесёт вам пользы. Вылей её за борт, Биэйн. Вылей её за борт». Биэйн, всё ещё держа крышку в обеих руках, взглянул в наветренную сторону. Затем он пошёл к поручням и бросил её со всем содержимым в море. На расстоянии меньше [[мили|миля]] к нам шёл ваш корабль под всеми парусами. Матросы были так поглощены тем, что происходило на палубе, что никто не поднял глаз, чтобы взглянуть на море. Теперь же вся команда следила за тем, как приближалось спасительное судно.
btn 33, — Были ли отданы какие-то приказы капитаном?
btn 33, — Как себя вёл Горман?
end

:33
pln — По началу никто не промолвил ни слова, но когда корабль стал в [[кабельтове|кабельтов]], капитан пришёл в себя и приказал набросить брезент на тело О'Брайена. От вас была спущена шлюпка, которая направилась к «Френсису Спейту», и в этот момент Горман засмеялся. Вначале он смеялся тихо, но с каждым ударом вёсел его смех становился всё громче…
pln — Да, капитан. Этот исступлённый смех помешанного встретил нашу шлюпку, когда она подошла к борту и я поднялся на палубу. — вмешался старший помощник, после чего еле слышно пробормотал. — [[Quos Iupiter perdere vult, dementat]].
pln — Вот так всё и произошло, сэр. — твёрдо закончил свой рассказ юнга и поднял ожидающий взгляд.
btn 34, — Послушай, Джордж. Я понимаю, что ты чувствуешь, но когда люди оказываются в такой ситуации в море, они подчиняются особому закону, и лишь бог судья тем, кто остаётся в живых и возвращается на сушу. Тебе надо хорошенько отдохнуть. Ступай спать.
btn 34, — Успокойся, Джордж, всё уже позади. Тебе надо отдохнуть. Ступай спать, только зайди, пожалуйста, по дороге к корабельному плотнику и передай ему, чтобы он приготовил к завтрашнему дню побольше прочной верёвки.
btn 34, — Сдаётся мне, малец, у вас в команде не было праведников, вот только ты хочешь искупить свои грехи за счёт товарищей. Хватит тратить моё время. Пошёл вон, щенок!
end

:34
pln <div class="francis_spaight_author">***</div>
pln Francis Spaight был транспортным кораблём XIX века, которым владел купец из ирландского города Лимерик. Судно занималось торговлей с Северной Америкой, перевозя туда ирландских эмигрантов и транспортируя древесину на обратном пути. Francis Spaight стал известен из-за случая каннибализма, произошедшего после опрокидывания корабля во время снежной бури у побережья Канады 3 декабря 1836 года, когда за борт была смыта вся провизия, а запасы воды оказались испорчены. Из 18 моряков трое были выброшены за борт во время аварии, а 15 выжили, включая четырёх юнг в подростковом возрасте.
btn 35, Далее
end

:35
pln Имея только дождевую воду и несколько бутылок вина, члены экипажа переносили сильный холод, обезвоживание и голод до 18 декабря, когда капитан предложил пожертвовать одним из четырёх юнг, чтобы выжить. Он обосновал исключение других старших членов экипажа тем, что они имели семьи, которые от них зависели. Четверо мальчиков приняли участие в жеребьёвке, и Патрик О'Брайен, пятнадцатилетний юнга, вытащил смертельный жребий.
btn 36, Далее
end

:36
pln Капитан приказал, чтобы кок убил О'Брайена; тот сначала отказался, но позже был принуждён капитаном. Однако кок не смог убить О'Брайена. О'Брайен попытался покончить с собой, порезав себе запястье, но его попытка также не удалась. В конце концов, капитан прибег к перерезанию горла О'Брайену, и в этот момент О'Брайен сопротивлялся, но был убит. Оставшиеся в живых члены экипажа каннибализировали О'Брайена в течение трёх дней до 20 декабря, когда взрослый матрос, Майкл Бейн, и ещё один юнга, Джордж Бёрнс, стали невменяемыми от обезвоживания и голода. Они были также каннибализированы, вместе с другим матросом, который умер в тот же день.
btn 37, Далее
end

:37
pln Когда выжившие собирались бросить ещё один жребий для каннибализма, их заметило американское судно Agenoria. моряки размахивали руками и ногами жертв людоедства, чтобы выразить своё бедственное положение. После двадцатидневного дрейфа выжило 11 членов экипажа из 18. Трое погибли из-за снежной бури, а четверо были каннибализированы. Поднятые на борт Agenoria были настолько измождены и истощены, что не могли есть без посторонней помощи.
pln В 1911 году Джек Лондон издал свой рассказ «Френсис Спейт», основой для которого послужило это происшествие. При литературной обработке история получила ряд изменений, вероятно с целью получения большего драматизма. Данная интерактивная версия рассказа об этих событиях основана на произведении Джека Лондона, поэтому также не является полностью исторически достоверной.
btn 38, Далее
end

:38
pln При оценке тех событий следует соблюдать принцип историзма, предполагающий анализ объектов исследования в связи с конкретно-историческими условиями их существования. Так, в то время каннибализм в подобных ситуациях считался вполне допустимым, по крайней мере, в европейском обществе. Важную роль играл неписанный морской закон — так называемый custom of the sea, являвшийся источником права на ровне с формализованными правилами. Согласно морскому обычаю, находящимся перед угрозой голодной смерти морякам дозволялся каннибализм, при условии единодушного согласия всех умирающих от голода и бросания жребия. Жребий бросался до тех пор, пока не оставался последний выживший, или не приходило спасение. И это признавалось не только допустимым с моральной точки зрения, но и законным по морскому обычаю, а спасшиеся моряки не подвергались за подобные действия судебному преследованию.
btn 39, Далее
end

:39
pln Перелом наступил лишь после суда над членами экипажа яхты Mignonette: процесс в английском суде в 1884 году, известный как R v Dudley and Stephens, в результате которого двое человек были приговорены к смертной казни, в последствии заменённой на шесть месяцев тюрьмы. Это стало закреплением прецедента, который устанавливает, что крайняя необходимость не включает в себя возможность убить и съесть человека даже ради спасения жизни других людей. При всём этом, в законодательстве многих стран до сих пор отсутствует прямое наказание за каннибализм, а судят людей обычно за убийство или надругательство над трупом.
btn 40, Далее
end

:40
pln ##9$И моряк-бунтарь
pln ##9$Жертвой выбран был,
pln ##9$Пальцем тронул сталь
pln ##9$И сам вены вскрыл…
pln 
pln Да, мы остались в живых.
pln Та кровь нас от смерти спасла.
pln Но что, что мы скажем святым,
pln Спустив шлюпки на небеса?!
pln 
pln ##9$Что в последний миг
pln ##9$Он открыл глаза,
pln ##9$Крикнул нам из тьмы:
pln ##9$«Впереди земля!»
pln 
pln <div class="francis_spaight_right">© М. А. Пушкина</div>
btn 0, Конец
end

:юнга
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Юнга — подросток на судне, готовящийся в матросы.</div>
pln <hr>
goto 1

:крюйсель
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Крюйсель (или крюйс-марсель) — второй снизу прямой парус на задней мачте (бизань-мачте).</div>
pln <hr>
goto 4

:подветренная сторона
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Подветренная сторона — сторона, противоположная той, откуда дует ветер.</div>
pln <hr>
goto 4

:комингсы люка
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Комингсы люка — сплошное вертикальное ограждение люка в целях лучшей защиты его от заливания водой и предохранения людей от падения в трюм.</div>
pln <hr>
goto 4

:наветренный борт
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Наветренный борт — борт судна, в который дует ветер.</div>
pln <hr>
goto 4

:ванты
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Ванты — снасти, удерживающие с боков мачты и их верхние продолжения — стеньги. Стоящие рядом тросы — вантины, образующие ванты, соединяются горизонтальными ступеньками — выбленками, по которым матросы поднимаются на мачты.</div>
pln <hr>
if previous_loc = "4.0" or previous_loc = "5" then goto 4.2
if count_6.1 < 1 then goto 7 else goto 6.1

:полубак
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Полубак — возвышенный уступ (надстройка) в носовой части судна. Под полубаком обычно располагались жилые помещения для матросов.</div>
pln <hr>
goto 6

:фок-мачта
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Фок-мачта — передняя мачта на двух- и более мачтовом судне.</div>
pln <hr>
goto 6

:грот-мачта
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Грот-мачта — на двух- и трёхмачтовом судне вторая от носа мачта.</div>
pln <hr>
goto 6

:крюйс-стеньга
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Крюйс-стеньга — верхнее продолжение самой задней мачты (бизань-мачты) на трёх- и более мачтовом судне.</div>
pln <hr>
goto 6

:бизань-мачта
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Бизань-мачта — самая задняя мачта на трёх- и более мачтовом судне.</div>
pln <hr>
if count_6.1 < 1 then goto 7 else goto 6.1

:фальшборт
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Фальшборт — ограждение по краям наружной палубы судна.</div>
pln <hr>
if count_6.1 < 1 then goto 7 else goto 6.1

:марс
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Марс — на парусных судах площадка, устраиваемая в месте соединения мачты с её верхним продолжением — стеньгой — и служащая для разноса в сторону бортов судна вант, удерживающих последнюю.</div>
pln <hr>
goto 7.1

:бизань-ванты
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Бизань-ванты — снасти, удерживающие с боков заднюю мачту судна (бизань-мачту).</div>
pln <hr>
goto 7.1

:каютный настил
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Каютный настил — палуба, пол в каюте.</div>
pln <hr>
if count_8.1 < 1 then goto 9 else goto 8.1

:ют
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Ют — кормовая надстройка судна или кормовая часть верхней палубы.</div>
pln <hr>
if count_11.2 < 1 then goto 12 else goto 11.2

:Sua tunica propior pallio
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Sua tunica propior pallio (своя туника ближе, чем плащ — лат.) — фраза из пьесы комедиографа Плавта (ок. 254 — 184 до н. э.); в русском языке эквивалентом является пословица «своя рубашка ближе к телу».</div>
pln <hr>
goto 16.1.1.3

:кок
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Кок — судовой повар.</div>
pln <hr>
if count_17.1 < 1 then goto 18 else goto 17.1

:миля
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Миля (морская) — единица расстояния на море, равная одной минуте (1##8242$) дуги земного меридиана, или 1852 метрам. В США длина мили принимается равной 1853,2 метра для более точного соответствия целому числу футов.</div>
pln <hr>
if count_31.1 < 1 then goto 32 else goto 31.1

:кабельтов
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Кабельтов — одна десятая часть морской мили равная 185,2 метра.</div>
pln <hr>
goto 33

:Quos Iupiter perdere vult, dementat
pln <div class="francis_spaight_note" role="complementary" aria-label="Сноска">Quos Iupiter perdere vult, dementat (кого Юпитер хочет погубить, того лишает разума — лат.) — слова неизвестного древнегреческого драматурга-трагика эпохи Софокла (ок. 496 — ок. 406 до н. э.).</div>
pln <hr>
goto 33
